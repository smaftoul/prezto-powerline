#
# A ZSH theme based on a combination of the skwp prezto theme and the robl ohmyzsh theme.
#  * env (ruby, node) info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#  * Time since last commit on the left
#
# Authors:
#   Tim Taylor <tim@tool-man.org>
#   David Rice <me@davidjrice.co.uk>

pmodload 'helper'

function prompt_powerline_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_powerline_pwd="$MATCH"
    unset MATCH
  else
    _prompt_powerline_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_powerline_time_since_commit {
  _prompt_powerline_time_since_commit=''
  if $(git rev-list -n 1 HEAD &>/dev/null) ; then
    local lc_time=$(git show -s --format='%at' 2>/dev/null)
    local seconds_since_last_commit=$((${EPOCHSECONDS} - lc_time))

    # totals
    local MINUTES=$((seconds_since_last_commit / 60))
    local HOURS=$((seconds_since_last_commit/3600))

    # sub-hours and sub-minutes
    local DAYS=$((seconds_since_last_commit / 86400))
    local SUB_HOURS=$((HOURS % 24))
    local SUB_MINUTES=$((MINUTES % 60))

    if [ "$HOURS" -gt 24 ]; then
      _prompt_powerline_time_since_commit='%F{21}'${DAYS}'%F{7}d%F{21}'${SUB_HOURS}'%F{7}h%F{21}'${SUB_MINUTES}'%F{7}m%f'
    elif [ "$MINUTES" -gt 60 ]; then
      _prompt_powerline_time_since_commit='%F{21}'${HOURS}'%F{7}h%F{21}'${SUB_MINUTES}'%F{7}m%f'
    else
      _prompt_powerline_time_since_commit='%F{21}'${MINUTES}'%F{7}m%f'
    fi
  fi
}

function prompt_powerline_env_versions {
  local -a versions
  versions=()
  if (( $+functions[node-info] )); then
    versions+=(${node_info[version]})
  fi
  if (( $+functions[ruby-info] )); then
    versions+=(${ruby_info[version]})
  fi
  _prompt_powerline_env_versions=${(j: %F{19}ÓÇ≥ %F{18}:)versions}
}

function prompt_powerline_precmd {
  local fmt_branch

  if (( $+functions[git-info] )); then
    git-info
  fi
  if (( $+functions[node-info] )); then
    node-info
  fi
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  prompt_powerline_pwd
  prompt_powerline_time_since_commit
  prompt_powerline_env_versions
}

function prompt_powerline_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  zmodload zsh/datetime
  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_powerline_precmd

  # Use extended color pallete if available.
  case $TERM in
    *256color*|*rxvt)
    __PROMPT_SKWP_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
    ;;
    *)
    __PROMPT_SKWP_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
    ;;
  esac

  zstyle ':prezto:module:git:info:branch' format 'ÓÇ† %b'
  zstyle ':prezto:module:git:info:commit' format '‚û¶ %.7c'
  zstyle ':prezto:module:git:info:position' format '%p'
  zstyle ':prezto:module:git:info:action' format ' %F{21}ÓÇ± %F{15}%s'
  zstyle ':prezto:module:git:info:clean' format ' ‚úî'
  zstyle ':prezto:module:git:info:indexed' format ' ‚óè'
  zstyle ':prezto:module:git:info:unindexed' format ' ‚úö'
  zstyle ':prezto:module:git:info:untracked' format ' ‚Ä¶'
  zstyle ':prezto:module:git:info:stashed' format ' ‚öë'
  zstyle ':prezto:module:git:info:keys' format \
    'ref' '$(coalesce "%b" "%p" "%c")' \
    'status' '%C$(coalesce "%I" "%i" "%u" "%S")%s'

  zstyle ':prezto:module:ruby:info:version' format 'üíé %v'
  zstyle ':prezto:module:node:info:version' format 'üöÄ %v'

  PROMPT='%F{15}%K{2} ${_prompt_powerline_pwd} %F{2}%K{4}'$'\uE0B0''%F{15}%K{4} ${(e)git_info[ref]}${(e)git_info[status]} %F{4}%K{18}'$'\uE0B0''%f%K{18} ${_prompt_powerline_time_since_commit} %k%F{18}'$'\uE0B0''%f '
  RPROMPT='%F{7}'$'\uE0B2''%F{18}%K{7} ${_prompt_powerline_env_versions} %f%k'
}

prompt_powerline_setup "$@"
