#
# A ZSH theme based on a combination of the skwp prezto theme and the robl ohmyzsh theme.
#  * env (ruby, node) info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#  * Time since last commit on the left
#
# Authors:
#   David Rice <me@davidjrice.co.uk>

function prompt_powerline_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_powerline_pwd="$MATCH"
    unset MATCH
  else
    _prompt_powerline_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_powerline_git_dir {
  _prompt_powerline_git_dir=${$(git rev-parse --git-dir 2>/dev/null):A}
}

function prompt_powerline_time_since_commit {
  _prompt_powerline_time_since_commit=''
  if [[ ! -z "${_prompt_powerline_git_dir}" ]] ; then
    if $(git rev-list -n 1 HEAD &>/dev/null) ; then
      local lc_time=$(git show -s --format='%at' 2>/dev/null)
      local seconds_since_last_commit=$((${EPOCHSECONDS} - lc_time))

      # totals
      local MINUTES=$((seconds_since_last_commit / 60))
      local HOURS=$((seconds_since_last_commit/3600))

      # sub-hours and sub-minutes
      local DAYS=$((seconds_since_last_commit / 86400))
      local SUB_HOURS=$((HOURS % 24))
      local SUB_MINUTES=$((MINUTES % 60))

      if [ "$HOURS" -gt 24 ]; then
        _prompt_powerline_time_since_commit='%F{21}'${DAYS}'%F{7}d%F{21}'${SUB_HOURS}'%F{7}h%F{21}'${SUB_MINUTES}'%F{7}m%f'
      elif [ "$MINUTES" -gt 60 ]; then
        _prompt_powerline_time_since_commit='%F{21}'${HOURS}'%F{7}h%F{21}'${SUB_MINUTES}'%F{7}m%f'
      else
        _prompt_powerline_time_since_commit='%F{21}'${MINUTES}'%F{7}m%f'
      fi
    fi
  fi
}

function prompt_powerline_env_version {
  _prompt_powerline_env_version=
  if [[ ! -z "${_prompt_powerline_git_dir}" ]] ; then
    _prompt_powerline_env_version=$(cat /dev/null "${_prompt_powerline_git_dir}"/../.*-version(N[1]) 2>/dev/null)
  fi
}

function prompt_powerline_precmd {
  # Check for untracked files or updated submodules since vcs_info doesn't.
  if [[ ! -z $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    fmt_branch=$'\ue0a0'" %b%u%c${__PROMPT_SKWP_COLORS[4]}●%f"
  else
    fmt_branch=$'\ue0a0'" %b%u%c"
  fi
  zstyle ':vcs_info:*:prompt:*' formats "${fmt_branch}"

  vcs_info 'prompt'
  prompt_powerline_pwd
  prompt_powerline_git_dir
  prompt_powerline_time_since_commit
  prompt_powerline_env_version
}

function prompt_powerline_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  zmodload zsh/datetime
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_powerline_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_SKWP_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_SKWP_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Enable VCS systems you use.
  zstyle ':vcs_info:*' enable bzr git hg svn

  # check-for-changes can be really slow.
  # You should disable it if you work with large repositories.
  zstyle ':vcs_info:*:prompt:*' check-for-changes true

  # Formats:
  # %b - branchname
  # %u - unstagedstr (see below)
  # %c - stagedstr (see below)
  # %a - action (e.g. rebase-i)
  # %R - repository path
  # %S - path in the repository
  # %n - user
  # %m - machine hostname

  # local fmt_branch="(${__PROMPT_SKWP_COLORS[1]}%b%f%u%c)"
  local fmt_branch="%F{18}"$'\ue0a0'" %b%f%u%c"
  local fmt_action="${__PROMPT_SKWP_COLORS[5]}%a%f"
  local fmt_unstaged="${__PROMPT_SKWP_COLORS[2]}●%f"
  local fmt_staged="${__PROMPT_SKWP_COLORS[5]}●%f"

  zstyle ':vcs_info:*:prompt:*' unstagedstr   "${fmt_unstaged}"
  zstyle ':vcs_info:*:prompt:*' stagedstr     "${fmt_staged}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${fmt_branch}${fmt_action}"
  zstyle ':vcs_info:*:prompt:*' formats       "${fmt_branch}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  PROMPT='%F{15}%K{2} ${_prompt_powerline_pwd} %F{2}%K{4}'$'\uE0B0''%F{15}%K{4} ${vcs_info_msg_0_} %F{4}%K{18}'$'\uE0B0''%f%K{18} ${_prompt_powerline_time_since_commit} %k%F{18}'$'\uE0B0''%f '
  RPROMPT='%F{21}'$'\uE0B2''%F{19}%K{21} ${_prompt_powerline_env_version} %f%k'
}

prompt_powerline_setup "$@"
