#
# A ZSH theme inspired by VIM Powerline that includes‚Ä¶
#
#  * ‚Ä¶ a single line prompt
#  * ‚Ä¶ project environment (java, node, ruby) and version (node, ruby)
#  * ‚Ä¶ compact git status and time since last commit
#  * ‚Ä¶ most recent history event
#  * ‚Ä¶ indication of non-zero exit status
#  * ‚Ä¶ base16 compatible color scheme
#
# Authors:
#   Tim Taylor <tim@tool-man.org>
#   David Rice <me@davidjrice.co.uk>

pmodload 'helper'

typeset -Ag _PROMPT_POWERLINE_BASE16
_PROMPT_POWERLINE_BASE16=(
  00  0
  08  1
  0B  2
  0A  3
  0D  4
  0E  5
  0C  6
  05  7
  03  8
  08  9
  0B 10
  0A 11
  0D 12
  0E 13
  0C 14
  07 15
  09 16
  0F 17
  01 18
  02 19
  04 20
  06 21
)
typeset -Agr _PROMPT_POWERLINE_BASE16

function prompt_powerline__expand_base16() {
  setopt LOCAL_OPTIONS
  setopt EXTENDED_GLOB

  REPLY=${REPLY//(#b)%([FK])\{(0[0-9a-fA-F])\}/%$match[1]'{'${_PROMPT_POWERLINE_BASE16[$match[2]]}'}'}
  return 0
}

function prompt_powerline__segment() {
  local bg fg sep_fg
  bg=${1:+%K{$1}}
  : ${bg:=%k}
  fg=${2:+%F{$2}}
  : ${fg:=%f}

  if [[ ${CURRENT_BG-unset} != 'unset' && $1 != $CURRENT_BG ]]; then
    sep_fg="%F{${CURRENT_BG:-00}}"
    REPLY+=" %{$bg$sep_fg%}ÓÇ∞%{$fg%} "
  else
    REPLY+="%{$bg$fg%}"
    if [[ ${CURRENT_BG-unset} == 'unset' ]] ; then
      REPLY+=' '
    fi
  fi
  [[ -n $3 ]] && REPLY+=$3
  CURRENT_BG=$1
  CURRENT_FG=$2
  return 0
}

function prompt_powerline__end() {
  if [[ -n $CURRENT_BG ]]; then
    REPLY+=" %{%k%{%F{$CURRENT_BG}%}%}ÓÇ∞%{%f%} "
  else
    REPLY+='%{%k%f%} '
  fi
  unset CURRENT_BG
  unset CURRENT_FG
}

function rprompt_powerline__segment() {
  local bg fg sep_fg
  bg=${1:+%K{$1}}
  : ${bg:=%k}
  fg=${2:+%F{$2}}
  : ${fg:=%f}

  if [[ $1 != $CURRENT_BG ]]; then
    sep_fg="%F{${1:-00}}"
    REPLY+=" %{$sep_fg%}ÓÇ≤%{$bg$fg%} "
  elif [[ ${CURRENT_BG-unset} != 'unset' ]] ; then
    REPLY+="%{$bg$fg%}"
  fi
  [[ -n $3 ]] && REPLY+=$3
  CURRENT_BG=$1
  CURRENT_FG=$2
  return 0
}

function rprompt_powerline__end() {
  REPLY+=' %{%k%f%}'
  unset CURRENT_BG
  unset CURRENT_FG
  return 0
}

function prompt_powerline__git_elapsed() {
  local latest remainder days hours minutes

  latest=$(($(git show -s --format='%at' 2>/dev/null) + 0))
  if ((latest > 0)) ; then
    remainder=$((EPOCHSECONDS - latest))
    if ((remainder > 86400)) ; then
      days=$((remainder / 86400))
      remainder=$((remainder % 86400))
      REPLY+="%{%F{06}%}${days}%{%F{05}%}d"
    fi
    if ((remainder > 3600)) ; then
      hours=$(($remainder / 3600))
      remainder=$((remainder % 3600))
      REPLY+="%{%F{06}%}${hours}%{%F{05}%}h"
    fi
    minutes=$((remainder / 60))
    REPLY+="%{%F{06}%}${minutes}%{%F{05}%}m"
  fi
  return 0
}

function prompt_powerline__envs() {
  local -a versions sep
  versions=()
  if [[ $+node_info[version] > 0 && -n $(node-app-root) ]]; then
    versions+=(${node_info[version]})
  fi
  if [[ $+ruby_info[version] > 0 && -n $(ruby-app-root) ]]; then
    versions+=(${ruby_info[version]})
  fi
  if (( $+functions[java-app-root] )) && java-app-root &>/dev/null ; then
    versions+='‚òï ' # no standard rvm/rbenv/npm/nodenv for Java
  fi
  sep=' %{%F{02}%}ÓÇ≥%{%F{05}%} '
  REPLY="${(pj:$sep:)versions}"
  return 0
}

function prompt_powerline_precmd() {
  local REPLY

  if (( $+functions[git-info] )); then
    git-info
  fi
  if (( $+functions[node-info] )); then
    node-info
  fi
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  REPLY=''
  prompt_powerline__git_elapsed
  prompt_powerline__expand_base16
  _prompt_powerline_git_elapsed=$REPLY

  REPLY=''
  prompt_powerline__envs
  prompt_powerline__expand_base16
  _prompt_powerline_envs=$REPLY
  return 0
}

function prompt_powerline_setup() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  local CURRENT_BG CURRENT_FG REPLY
  unset CURRENT_BG
  unset CURRENT_FG

  zmodload zsh/datetime
  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_powerline_precmd

  zstyle ':prezto:module:git:info:branch'    format 'ÓÇ† %b'
  zstyle ':prezto:module:git:info:commit'    format '‚û¶ %.7c'
  zstyle ':prezto:module:git:info:position'  format '%p'
  zstyle ':prezto:module:git:info:action'    format '%s'
  zstyle ':prezto:module:git:info:action:cherry-pick-sequence' format 'cherry-pick'
  zstyle ':prezto:module:git:info:action:rebase-interactive'   format 'rebase'
  zstyle ':prezto:module:git:info:action:rebase-merge'         format 'rebase'
  zstyle ':prezto:module:git:info:indexed'   format ' ‚¶ø'
  zstyle ':prezto:module:git:info:unindexed' format ' ‚óè'
  zstyle ':prezto:module:git:info:untracked' format ' ‚óã'
  zstyle ':prezto:module:git:info:unmerged'  format ' üí• '
  zstyle ':prezto:module:git:info:added'     format ' ‚¶ø'
  zstyle ':prezto:module:git:info:modified'  format ' ‚óè'
  zstyle ':prezto:module:git:info:deleted'   format ' ‚óè'
  zstyle ':prezto:module:git:info:renamed'   format ' ‚¶ø'
  zstyle ':prezto:module:git:info:stashed'   format ' ‚â°'
  zstyle ':prezto:module:git:info:ahead'     format '‚Üë%A'
  zstyle ':prezto:module:git:info:behind'    format '%B‚Üì'
  zstyle ':prezto:module:git:info:diverged'  format '%B‚áµ%A'
  zstyle ':prezto:module:git:info:keys'      format \
    'action' '%s' \
    'ref' '$(coalesce "%b" "%p" "%c")' \
    'status' '$(coalesce "%U" "%u" "%I" "%m" "%d" "%i" "%a" "%r")%S' \
    'tracking' '%V%B%A'

  zstyle ':prezto:module:ruby:info:version' format 'üíé %v'
  zstyle ':prezto:module:node:info:version' format 'üöÄ %v'

  REPLY=''
  prompt_powerline__segment '%(?:2:1)' 07 '%~' # TODO: support base16 within conditional, e.g. %(?:0B:08)
  prompt_powerline__segment 02 07 '${(e)git_info[ref]}'
  prompt_powerline__segment 02 07 '${(e)git_info[status]}'
  prompt_powerline__segment 02 07 '${(e)${git_info[tracking]:+" %F{05}ÓÇ±%F{07} $git_info[tracking]"}}'
  prompt_powerline__segment 02 07 '${(e)${git_info[action]:+" %F{05}ÓÇ±%F{07} $git_info[action]"}}'
  prompt_powerline__segment 01 07 '${_prompt_powerline_git_elapsed}'
  prompt_powerline__end
  prompt_powerline__expand_base16
  PROMPT=$REPLY

  REPLY=''
  rprompt_powerline__segment 01 05 '${_prompt_powerline_envs}'
  rprompt_powerline__segment 06 01 '%(?::?%? %F{05}ÓÇ≥%F{01} )'
  rprompt_powerline__segment 06 02 '!%!'
  rprompt_powerline__end
  prompt_powerline__expand_base16
  RPROMPT=$REPLY

  return 0
}

prompt_powerline_setup "$@"
